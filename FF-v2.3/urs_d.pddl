(define (domain urs)
  (:requirements :strips :typing :equality :adl)
  (:types drone loc)
  (:predicates
    (above ?l0 ?l1 - loc)
    (aligned ?l0 ?l1 - loc)
    (at ?d - drone ?l - loc)
    (closest ?d - drone ?l - loc)
    (collided ?l0 ?l1 - loc)
    (hovered ?d - drone)
    (oriented ?d - drone)
    (scanned ?l - loc)
  )
  (:action ascend
    :parameters (?d - drone ?from ?to - loc)
    :precondition (and
      (above ?to ?from)
      (aligned ?from ?to)
      (at ?d ?from)
      (not (exists (?x - drone ?y - loc)
        (and
          (not (= ?x ?d))
          (at ?x ?y)
          (collided ?to ?y)
        )
      ))
      (hovered ?d)
    )
    :effect (and
      (not (at ?d ?from))
      (at ?d ?to)
    )
  )
  (:action descend
    :parameters (?d - drone ?from ?to - loc)
    :precondition (and
      (above ?from ?to)
      (aligned ?from ?to)
      (at ?d ?from)
      (not (exists (?x - drone ?y - loc)
        (and
          (not (= ?x ?d))
          (at ?x ?y)
          (collided ?to ?y)
        )
      ))
      (hovered ?d)
    )
    :effect (and
      (not (at ?d ?from))
      (at ?d ?to)
    )
  )
  (:action land
    :parameters (?d - drone)
    :precondition (hovered ?d)
    :effect (and (not (hovered ?d)))
  )
  (:action move
    :parameters (?d - drone ?from ?to - loc)
    :precondition (and
      (at ?d ?from)
      (not (exists (?x - drone ?y - loc)
        (and
          (not (= ?x ?d))
          (at ?x ?y)
          (collided ?to ?y)
        )
      ))
      (hovered ?d)
      (not (aligned ?from ?to))
    )
    :effect (and
      (not (at ?d ?from))
      (at ?d ?to)
    )
  )
  (:action rotate
    :parameters (?d - drone)
    :precondition (and
      (not (oriented ?d))
      (hovered ?d)
    )
    :effect (and (oriented ?d))
  )
  (:action scan
    :parameters (?d  - drone ?l - loc)
    :precondition (and
      (closest ?d ?l)
      (hovered ?d)
      (not (scanned ?l))
    )
    :effect (and (scanned ?l))
  )
  (:action takeoff
    :parameters (?d - drone)
    :precondition (not (hovered ?d))
    :effect (and (hovered ?d))
  )
)
